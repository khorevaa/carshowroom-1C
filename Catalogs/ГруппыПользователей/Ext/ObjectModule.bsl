#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем СтарыйРодитель; // Значение родителя группы до изменения для использования
                      // в обработчике события ПриЗаписи.

Перем СтарыйСоставГруппыПользователей; // Состав пользователей группы пользователей
                                       // до изменения для использования в обработчике
                                       // события ПриЗаписи.

Перем ЭтоНовый; // Показывает, что был записан новый объект.
                // Используются в обработчике события ПриЗаписи.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверенныеРеквизитыОбъекта	= Новый Массив;
	Ошибки						= Неопределено;

	// Проверка использования родителя.
	Если Родитель = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		БазоваяПодсистемаКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
			"Объект.Родитель",
			"Предопределенная группа ""Все пользователи"" не может быть родителем.",
			"");
	КонецЕсли;

	// Проверка незаполненных и повторяющихся пользователей.
	ПроверенныеРеквизитыОбъекта.Добавить("Состав.Пользователь");

	Для каждого ТекущаяСтрока Из Состав Цикл;
		НомерСтроки = Состав.Индекс(ТекущаяСтрока);

		// Проверка заполнения значения.
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Пользователь) Тогда
			БазоваяПодсистемаКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"Объект.Состав[%1].Пользователь",
				"Пользователь не выбран.",
				"Объект.Состав",
				НомерСтроки,
				"Пользователь в строке %1 не выбран.");
			Продолжить;
		КонецЕсли;

		// Проверка наличия повторяющихся значений.
		НайденныеЗначения = Состав.НайтиСтроки(Новый Структура("Пользователь", ТекущаяСтрока.Пользователь));
		Если НайденныеЗначения.Количество() > 1 Тогда
			БазоваяПодсистемаКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"Объект.Состав[%1].Пользователь",
				"Пользователь повторяется.",
				"Объект.Состав",
				НомерСтроки,
				"Пользователь в строке %1 повторяется.");
		КонецЕсли;
	КонецЦикла;

	БазоваяПодсистемаКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);

	БазоваяПодсистемаСервер.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ПроверенныеРеквизитыОбъекта);
КонецПроцедуры

// Блокирует недопустимые действия с предопределенной группой "Все пользователи".
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ЭтоНовый = ЭтоНовый();

	Если Ссылка = Справочники.ГруппыПользователей.ВсеПользователи Тогда
		Если НЕ Родитель.Пустая() Тогда
			ВызватьИсключение "Предопределенная группа ""Все пользователи"" может быть только в корне.";
		КонецЕсли;
		Если Состав.Количество() > 0 Тогда
			ВызватьИсключение "Добавление пользователей в группу ""Все пользователи"" не поддерживается.";
		КонецЕсли;
	Иначе
		Если Родитель = Справочники.ГруппыПользователей.ВсеПользователи Тогда
			ВызватьИсключение "Предопределенная группа ""Все пользователи"" не может быть родителем.";
		КонецЕсли;

		СтарыйРодитель = ?(Ссылка.Пустая(), Неопределено, БазоваяПодсистемаСервер.ЗначениеРеквизитаОбъекта(Ссылка, "Родитель"));

		Если ЗначениеЗаполнено(Ссылка) И Ссылка <> Справочники.ГруппыПользователей.ВсеПользователи Тогда
			СтарыйСоставГруппыПользователей = БазоваяПодсистемаСервер.ЗначениеРеквизитаОбъекта(Ссылка, "Состав").Выгрузить();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	УчастникиИзменений = Новый Соответствие;
	ИзмененныеГруппы   = Новый Соответствие;

	Если Ссылка <> Справочники.ГруппыПользователей.ВсеПользователи Тогда
		ИзмененияСостава = ПользователиСервер.РазличияЗначенийКолонки("Пользователь", Состав.Выгрузить(), СтарыйСоставГруппыПользователей);

		ПользователиСервер.ОбновитьСоставыГруппПользователей(Ссылка, ИзмененияСостава, УчастникиИзменений, ИзмененныеГруппы);

		Если СтарыйРодитель <> Родитель Тогда
			Если ЗначениеЗаполнено(Родитель) Тогда
				ПользователиСервер.ОбновитьСоставыГруппПользователей(Родитель, , УчастникиИзменений, ИзмененныеГруппы);
			КонецЕсли;

			Если ЗначениеЗаполнено(СтарыйРодитель) Тогда
				ПользователиСервер.ОбновитьСоставыГруппПользователей(СтарыйРодитель, , УчастникиИзменений, ИзмененныеГруппы);
			КонецЕсли;
		КонецЕсли;

		ПользователиСервер.ОбновитьИспользуемостьСоставовГруппПользователей(Ссылка, УчастникиИзменений, ИзмененныеГруппы);
	КонецЕсли;

	ПользователиСервер.ПослеОбновленияСоставовГруппПользователей(УчастникиИзменений, ИзмененныеГруппы);

	ИнтеграцияПодсистемСервер.ПослеДобавленияИзмененияПользователяИлиГруппы(Ссылка, ЭтоНовый);
КонецПроцедуры

#КонецОбласти

#КонецЕсли