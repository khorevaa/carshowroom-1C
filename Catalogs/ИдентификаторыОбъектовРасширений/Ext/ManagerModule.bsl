#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Процедура обновляет данные справочника по метаданным конфигурации.
//
// Параметры:
//  ЕстьИзменения  - Булево - (возвращаемое значение) в этот параметр возвращается
//                   значение Истина, если производилась запись, иначе не изменяется.
//
//  ЕстьУдаленные  - Булево - (возвращаемое значение) в этот параметр возвращается
//                   значение Истина, если хотя бы один элемент справочника был помечен
//                   на удаление, иначе не изменяется.
//
//  ТолькоПроверка - Булево - не производить никаких изменений, а лишь выполнить
//                   установку флажков ЕстьИзменения, ЕстьУдаленные.
//
Процедура ОбновитьДанныеСправочника(ЕстьИзменения = Ложь, ЕстьУдаленные = Ложь, ТолькоПроверка = Ложь) Экспорт
	Справочники.ИдентификаторыОбъектовМетаданных.ВыполнитьОбновлениеДанных(ЕстьИзменения, ЕстьУдаленные, ТолькоПроверка, , , Истина);
КонецПроцедуры

// Возвращает Истина, если объект метаданных, которому соответствует
// идентификатор объектов расширений существует в справочнике и
// не помечен на удаление, но отсутствует в кэше метаданных расширений.
//
// Параметры:
//  Идентификатор - СправочникСсылка.ИдентификаторыОбъектовРасширений - идентификатор
//                    объекта метаданных расширения.
//
// Возвращаемое значение:
//  Булево - Истина, если отключен.
//
Функция ОбъектРасширенияОтключен(Идентификатор) Экспорт
	БазоваяПодсистемаСерверПовтИсп.ИдентификаторыОбъектовМетаданныхПроверкаИспользования(Истина, Истина);

	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	            	  |	ИСТИНА КАК ЗначениеИстина
	            	  |ИЗ
	            	  |	Справочник.ИдентификаторыОбъектовРасширений КАК Идентификаторы
	            	  |ГДЕ
	            	  |	Идентификаторы.Ссылка = &Ссылка
	            	  |	И НЕ Идентификаторы.ПометкаУдаления
	            	  |	И НЕ ИСТИНА В
	            	  |				(ВЫБРАТЬ ПЕРВЫЕ 1
	            	  |					ИСТИНА
	            	  |				ИЗ
	            	  |					РегистрСведений.ИдентификаторыОбъектовВерсийРасширений КАК ВерсииИдентификаторов
	            	  |				ГДЕ
	            	  |					ВерсииИдентификаторов.Идентификатор = Идентификаторы.Ссылка
	            	  |					И ВерсииИдентификаторов.ВерсияРасширений = &ВерсияРасширений)";
	Запрос.УстановитьПараметр("Ссылка", Идентификатор);
	Запрос.УстановитьПараметр("ВерсияРасширений", ПараметрыСеанса.ВерсияРасширений);

	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции

Функция ИдентификаторыОбъектовТекущейВерсииРасширенийЗаполнены() Экспорт
	Запрос			= Новый Запрос;
	Запрос.Текст	= "ВЫБРАТЬ ПЕРВЫЕ 1
	            	  |	ИСТИНА КАК ЗначениеИстина
	            	  |ИЗ
	            	  |	РегистрСведений.ИдентификаторыОбъектовВерсийРасширений КАК ВерсииИдентификаторов
	            	  |ГДЕ
	            	  |	ВерсииИдентификаторов.ВерсияРасширений = &ВерсияРасширений";
	Запрос.УстановитьПараметр("ВерсияРасширений", ПараметрыСеанса.ВерсияРасширений);

	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции

#КонецЕсли
